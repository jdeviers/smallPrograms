MODULE MOD_PROCEDURES_FCT
  implicit none

  contains

  REAL FUNCTION FCT_RMSD_VEC(COORDS_REF,COORDS)
    implicit none

    ! .. Parameters ..
    REAL,INTENT(IN)  :: COORDS_REF(:),COORDS(:)

    ! .. Internal vars ..
    INTEGER :: NBCOORDS,i
    REAL    :: ACCDIFF

    IF (UBOUND(COORDS_REF,1).NE.UBOUND(COORDS,1)) ERROR STOP "RMSD FAILED: MISMATCHED COORDS"
    NBCOORDS = UBOUND(COORDS_REF,1)

    ACCDIFF=0.
    DO i=1,NBCOORDS
      ACCDIFF=ACCDIFF+(COORDS(i)-COORDS_REF(i))**2.
    END DO
    FCT_RMSD_VEC = SQRT(ACCDIFF/NBCOORDS)

  END FUNCTION FCT_RMSD_VEC

  REAL FUNCTION FCT_RMSD_MAT(COORDS_REF,COORDS)
    implicit none

    ! .. Parameters ..
    REAL,INTENT(IN)  :: COORDS_REF(:,:),COORDS(:,:)

    ! .. Internal vars ..
    INTEGER :: NBCOORDS,NBDIMS,i,j
    REAL    :: LINEDIST,ACCDIFF

    IF (UBOUND(COORDS_REF,1).NE.UBOUND(COORDS,1)) ERROR STOP "RMSD FAILED: MISMATCHED COORDS"
    IF (UBOUND(COORDS_REF,2).NE.UBOUND(COORDS,2)) ERROR STOP "RMSD FAILED: MISMATCHED COORDS"
    NBCOORDS = UBOUND(COORDS_REF,1)
    NBDIMS =   UBOUND(COORDS_REF,2)

    ACCDIFF=0.
    DO i=1,NBCOORDS
      LINEDIST=0.
      DO j=1,NBDIMS
        LINEDIST=LINEDIST + (COORDS(i,j)-COORDS_REF(i,j))**2.
      END DO
      ACCDIFF=ACCDIFF+LINEDIST
    END DO
    FCT_RMSD_MAT = SQRT(ACCDIFF/NBCOORDS)

  END FUNCTION FCT_RMSD_MAT

  REAL FUNCTION FCT_COM_VEC(COORDS)
    implicit none

    ! .. Parameters ..
    REAL,INTENT(IN) :: COORDS(:)

    ! .. Internal vars ..
    INTEGER :: NBCOORDS,i
    REAL    :: ACCSUM
    
    NBCOORDS=UBOUND(COORDS,1)
    ACCSUM = 0.

    DO i=1,NBCOORDS
      ACCSUM = ACCSUM+COORDS(i)
    END DO
    FCT_COM_VEC = ACCSUM/NBCOORDS

  END FUNCTION FCT_COM_VEC

  FUNCTION FCT_COM_MAT(COORDS)
    implicit none

    ! .. Parameters ..
    REAL,INTENT(IN)   :: COORDS(:,:)
    REAL,DIMENSION(3) :: FCT_COM_MAT

    ! .. Internal vars ..
    INTEGER :: NBCOORDS,i
    REAL    :: ACCSUM(3)
    
    NBCOORDS=UBOUND(COORDS,1)
    ACCSUM = 0.

    DO i=1,NBCOORDS
      ACCSUM(:) = ACCSUM(:)+COORDS(i,:)
    END DO
    FCT_COM_MAT = ACCSUM/NBCOORDS

  END FUNCTION FCT_COM_MAT

END MODULE MOD_PROCEDURES_FCT